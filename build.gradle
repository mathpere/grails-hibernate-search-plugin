buildscript {
	ext { grailsVersion = project.grailsVersion }
	repositories {
		mavenLocal()
		maven { url "https://repo.grails.org/grails/core" }
	}
	dependencies {
		classpath "org.grails:grails-gradle-plugin:$grailsVersion"
		classpath "com.bertramlabs.plugins:asset-pipeline-gradle:2.8.2"
	}
}

version "2.1.1"
group "org.grails.plugins"

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"org.grails.grails-plugin"
apply plugin:"org.grails.grails-plugin-publish"
apply plugin:"org.grails.grails-gsp"
apply plugin:"asset-pipeline"

ext {
	grailsVersion = project.grailsVersion
	gradleWrapperVersion = project.gradleWrapperVersion
}

configurations {
	groovydocconf.extendsFrom(runtime)
}
groovydoc {
	def title = "Grails Hibernate Search plugin ${version}"
	groovyClasspath = project.configurations.groovydocconf
}

repositories {
	mavenLocal()
	maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
	imports { mavenBom "org.grails:grails-bom:$grailsVersion" }
	applyMavenExclusions false
}

dependencies {
	compile "org.springframework.boot:spring-boot-starter-logging"
	compile "org.springframework.boot:spring-boot-autoconfigure"
	compile "org.grails:grails-core"
	compile "org.springframework.boot:spring-boot-starter-actuator"
	compile "org.springframework.boot:spring-boot-starter-tomcat"
	compile "org.grails:grails-dependencies"
	compile "org.grails:grails-web-boot"
	compile "org.grails.plugins:cache"
	compile "org.grails.plugins:scaffolding"

	// GORM 6
	compile "org.grails.plugins:hibernate5:6.1.4"
	compile 'org.grails.plugins:cache'
	
	compile "org.hibernate:hibernate-core:5.2.9.Final"
	compile "org.hibernate:hibernate-ehcache:5.2.9.Final"
	
	compile('org.hibernate:hibernate-search-orm:5.7.0.Final')
	
	console "org.grails:grails-console"
	profile "org.grails.profiles:web-plugin"
	provided "org.grails:grails-plugin-services"
	provided "org.grails:grails-plugin-domain-class"
	runtime "com.bertramlabs.plugins:asset-pipeline-grails:2.8.2"
	testCompile "org.grails:grails-plugin-testing"
	testCompile "org.grails.plugins:geb"
	testRuntime "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
	testRuntime "net.sourceforge.htmlunit:htmlunit:2.18"
	
	groovydocconf 'org.fusesource.jansi:jansi:1.13'
	groovydocconf 'org.codehaus.groovy:groovy-all:2.4.10'

}

grailsPublish {
	// user/key: useless, see https://medium.com/@benorama/how-to-publish-your-grails-3-plugin-to-bintray-c341b24f567d#.elz43j5nq
	user = 'lgrignon'
//	key = '????'
	githubSlug = 'mathpere/grails-hibernate-search-plugin'
	license { name = 'Apache-2.0' }
	title = "Grails Hibernate Search Plugin"
	desc = "This plugin aims to integrate Hibernate Search features to Grails in very few steps."
	developers = [mathpere: "Mathieu Perez", lgrignon: "Louis Grignon"]
}

if (project.hasProperty('hibernateSearchMavenUser') && project.hasProperty('hibernateSearchMavenPassword') && project.hasProperty('hibernateSearchMavenRepoUrl')) {
	println "Configure publish tasks"
	publishing {
		repositories {
			maven {
				credentials {
					username "$hibernateSearchMavenUser"
					password "$hibernateSearchMavenPassword"
				}
				url "$hibernateSearchMavenRepoUrl/libs-release-local"
			}
		}
		publications {
			mavenJar(MavenPublication) {
				groupId 'org.grails.plugins'
				artifactId 'hibernate-search'

				pom.withXml {
					def pomNode = asNode()
					
					pomNode.appendNode('description',
						'Grails 3.2.x plugin for Hibernate Search')
					
					pomNode.dependencyManagement.replaceNode {}

					// simply remove dependencies without a version
					// version-less dependencies are handled with dependencyManagement
					// see https://github.com/spring-gradle-plugins/dependency-management-plugin/issues/8 for more complete solutions
					pomNode.dependencies.dependency.findAll {
						it.version.text().isEmpty()
					}.each {
						it.replaceNode {}
					}
				}

				from components.java
			}
		}
	}
}

assets { packagePlugin = true }

task dumpDependencies {
    doLast {
        configurations.compile.each { println it }
    }
}

